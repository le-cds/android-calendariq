plugins {
    id 'com.android.application'
    id 'dagger.hilt.android.plugin'
}

def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionRevision = 2
def versionSuffix = "devel"

android {
    defaultConfig {
        applicationId "net.hypotenubel.calendariq"

        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionRevision

        if (versionSuffix.isEmpty()) {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionRevision}"
        } else {
            versionName "${versionMajor}.${versionMinor}.${versionPatch}-${versionSuffix}-${versionRevision}"
        }

        resValue "string", "app_version", "${defaultConfig.versionName}"

        compileSdkVersion 30
        minSdkVersion 24
        targetSdkVersion 30

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        debug {
            debuggable true
        }

        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
    }

    flavorDimensions "emulator"
    productFlavors {
        onDevice {
            dimension "emulator"
        }

        onEmulator {
            dimension "emulator"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.activity:activity-ktx:1.1.0"
    implementation 'androidx.appcompat:appcompat:1.3.0-alpha02'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02'
    implementation 'androidx.hilt:hilt-work:1.0.0-alpha02'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation "androidx.navigation:navigation-fragment:2.3.2"
    implementation "androidx.navigation:navigation-ui:2.3.2"
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.2.0-beta01'
    implementation "androidx.room:room-runtime:2.2.6"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.work:work-runtime:2.4.0"
    implementation 'com.google.android.material:material:1.2.1'
    implementation "com.google.dagger:hilt-android:2.28-alpha"

    annotationProcessor 'androidx.hilt:hilt-compiler:1.0.0-alpha02'
    annotationProcessor "androidx.room:room-compiler:2.2.6"
    annotationProcessor "com.google.dagger:hilt-android-compiler:2.28-alpha"

    testImplementation 'junit:junit:4.13.1'

    androidTestImplementation "androidx.navigation:navigation-testing:2.3.2"
    androidTestImplementation 'androidx.test:core:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.ext:truth:1.3.0'
    androidTestImplementation 'com.google.truth:truth:1.0'
}
